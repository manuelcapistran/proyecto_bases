--TRIGGER PARA FALTAS
CREATE OR REPLACE TRIGGER TR_FALTA_PUNTOS_NEGATIVOS FOR
    INSERT ON FALTA
COMPOUND TRIGGER
    TYPE FALTA_TYPE IS RECORD(
        FOLIO FALTA.FOLIO%TYPE,
        PROPIETARIO_ID FALTA.PROPIETARIO_ID%TYPE,
        FECHA FALTA.FECHA%TYPE,
        DESCRIPCION FALTA.DESCRIPCION%TYPE,
        CANTIDAD_PUNTOS_NEGATIVOS FALTA.CANTIDAD_PUNTOS_NEGATIVOS%TYPE,
        DETALLE_FALTA FALTA.DETALLE_FALTA%TYPE
    );
    TYPE FALTAS_TYPE IS
        TABLE OF FALTA_TYPE;
    FALTAS FALTAS_TYPE := FALTAS_TYPE();
    BEFORE EACH ROW IS
        V_INDEX NUMBER;
    BEGIN
        FALTAS.EXTEND;
        V_INDEX:=FALTAS.LAST;
        FALTAS(V_INDEX).FOLIO := :NEW.FOLIO;
        FALTAS(V_INDEX).PROPIETARIO_ID := :NEW.PROPIETARIO_ID;
        FALTAS(V_INDEX).FECHA := :NEW.FECHA;
        FALTAS(V_INDEX).DESCRIPCION := :NEW.DESCRIPCION;
        FALTAS(V_INDEX).CANTIDAD_PUNTOS_NEGATIVOS := :NEW.CANTIDAD_PUNTOS_NEGATIVOS;
        FALTAS(V_INDEX).DETALLE_FALTA := :NEW.DETALLE_FALTA;
    END BEFORE EACH ROW;

    AFTER STATEMENT IS
        V_PUNTOS_NEGATIVOS_TOTALES FALTA.CANTIDAD_PUNTOS_NEGATIVOS%TYPE;
    BEGIN
        FOR I IN FALTAS.FIRST .. FALTAS.LAST LOOP
            SELECT
                NVL(SUM(CANTIDAD_PUNTOS_NEGATIVOS),0) INTO V_PUNTOS_NEGATIVOS_TOTALES
            FROM
                FALTA F
            WHERE
                F.PROPIETARIO_ID = FALTAS(I).PROPIETARIO_ID;
            IF V_PUNTOS_NEGATIVOS_TOTALES >= 200 THEN
                UPDATE PROPIETARIO
                SET
                    DERECHO_LICENCIA = FALSE
                WHERE
                    PROPIETARIO_ID = FALTAS(I).PROPIETARIO_ID;
                UPDATE LICENCIA
                SET
                    ES_ACTIVA = FALSE
                WHERE
                    PROPIETARIO_ID = FALTAS(I).PROPIETARIO_ID;
            END IF;
        END LOOP;
    END AFTER STATEMENT;
END;
/
SHOW ERRORS;



